# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=beatbox-bzr
pkgver=174
pkgrel=1
pkgdesc="An easy to use, stable, fast and good looking music library organizer written in Vala"
arch=('i686' 'x86_64')
url="https://launchpad.net/beat-box"
license=('GPL')
depends=('libgee' 'gtk2' 'gstreamer0.10' 'taglib' 'libxml2' 'gconf' 'libnotify' 'libunique' 'libsoup' 'json-glib' 'dbus-glib' 'sqlheavy-git')
makedepends=('bzr' 'vala' 'python')
install=${pkgname}.install
source=('fix_indentation.diff')
md5sums=('7ff0004f6bde844f4863eea1b7f71261')

_bzrtrunk="lp:beat-box"
_bzrmod="beatbox"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  patch -Np2 < "$srcdir"/fix_indentation.diff

  ./waf configure --prefix /usr
  ./waf build
}

package() {
  cd "$srcdir/$_bzrmod-build"

  ./waf install --destdir "$pkgdir/"
  mkdir -p "$pkgdir"/usr/share/man/man1
  cp debian/beatbox.1 "$pkgdir"/usr/share/man/man1
} 
