# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Marcelo Cavalcante / Kalib <kalib@archlinux-br.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gimmeapill <gimmeapill@gmail.com>
# Maintainer: M Rawash <mrawash@gmail.com>

pkgname=planeshift
pkgver=0.5.4
pkgrel=1
pkgdesc="Open source and cross-platform 3D Fantasy MMORPG"
arch=('i686' 'x86_64')

_arch="x86"
md5sums=('e3fa024f57d4f88786e6cfa817c5a0ea'
         '5be27f6e51c8edff591aa1fdc668b7e9'
         'e75381f677b5d2ae331d0e4e742ee21a'
         'dbac9cf5bfb63463bcb2b5ea09b00586'
         '9f9b8d4033fd1b2c7ef33c5e04207abb')
if [ $CARCH == "x86_64" ]; then
  _arch="x64"
  md5sums[0]='1ff1bad524080fa44b4c8b668ce2882a'
fi

url="http://www.planeshift.it/"
license=('GPL' 'custom:PlaneShift Content License')
depends=( 'libgl' 'libsm' 'libxmu' 'libxpm' 'openal')
options=('emptydirs')
install=planeshift.install
source=(http://kiwi.frubar.net/planeshift/${pkgver}/PlaneShift-v${pkgver}-${_arch}.bin \
	planeshift.desktop \
	planeshiftsetup.desktop \
	license.txt
  	scriptfix.patch)


build() {
  cd ${srcdir}

  # Make the install executable
  chmod +x PlaneShift-v${pkgver}-${_arch}.bin

  # Extract the files
  ./PlaneShift-v${pkgver}-${_arch}.bin \
  --mode unattended \
  --syswide yes \
  --prefix $pkgdir/opt

  # Set correct permissions for files and folders
  find $pkgdir/opt/PlaneShift -type f -exec chmod 660 {} +
  find $pkgdir/opt/PlaneShift -type d -exec chmod 770 {} +

  cd $pkgdir/opt/PlaneShift

  # Set correct permissions for executables
  chmod 770 psclient.bin pslaunch.bin psclient pslaunch

  # Install scripts to proper directory
  install -D -m755 psclient ${pkgdir}/usr/bin/$pkgname
  install -D -m755 pslaunch ${pkgdir}/usr/bin/$pkgname-setup

  # Remove unnecessary files
  rm uninstall unscript.sh planeshift

  cd $pkgdir/usr/bin/

  #fix script
  patch -Np0 < $srcdir/scriptfix.patch || return 1

  # Install shortcuts
  mkdir -p $pkgdir/usr/share/applications
  install -D -m644 $srcdir/*.desktop $pkgdir/usr/share/applications/

  # Install the PlaneShift Content License
  install -D -m644 $srcdir/license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
