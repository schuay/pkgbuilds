# $Id: PKGBUILD 77110 2010-04-10 18:15:33Z ibiru $
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=2.4.0
pkgrel=1
pkgdesc="Media player based on BMP"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libxml2' 'desktop-file-utils' 'audacious-plugins')
optdepends=('unzip: Zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=(http://distfiles.atheme.org/$pkgname-$pkgver.tgz)
sha256sums=('f2a2d45b9cb8fc24c1c55aef72a3a397a9a35776c18a94532b1bd3238577d5d8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr \
      --disable-sse2
  elif [[ $CARCH == "x86_64" ]]; then
    ./configure --prefix=/usr
  fi

  make
  make DESTDIR="$pkgdir" install

  #fix for: tarball installs softlinks that point into buildroot
  rm -f "$pkgdir/usr/bin/audtool"
  ln -s /usr/bin/audtool2 "$pkgdir/usr/bin/audtool"

  rm -f "$pkgdir/usr/bin/audacious"
  ln -s /usr/bin/audacious2 "$pkgdir/usr/bin/audacious"
}
