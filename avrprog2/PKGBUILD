# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=avrprog2
pkgver=1.3.1
pkgrel=1
pkgdesc="A Linux tool for the MikroElektronika AVRprog2 programming hardware."
arch=('x86_64' 'i686')
url="http://avrprog2.sourceforge.net/"
license=('GPL')
depends=('boost-libs' 'libusb')
makedepends=('doxygen' 'boost')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|-mt||g' configure.ac
  sed -i 's|BUS|SUBSYSTEM|' 60-avrpog.rules

  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

md5sums=('834ef2a32fd173bd82224f22b60279ec')

# vim:set ts=2 sw=2 et:
