# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=avrprog2
pkgver=1.3.1
pkgrel=2
pkgdesc="A Linux tool for the MikroElektronika AVRprog2 programming hardware."
arch=('x86_64' 'i686')
url="http://avrprog2.sourceforge.net/"
license=('GPL')
depends=('boost-libs' 'libusb')
makedepends=('doxygen' 'boost')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # There are no multithreaded boost libs in arch.
  sed -i 's|-mt||g' configure.ac
  # Deprecated udev syntax.
  sed -i 's|BUS|SUBSYSTEM|' 60-avrpog.rules
  # Rules dir has moved since systemd-tools.
  sed -i 's|/lib/udev/rules.d|/usr/lib/udev/rules.d|' Makefile.am
  # gcc 4.7 does not auto-include unistd.h
  sed -i '21 i\
    #include <unistd.h>' src/CAvrProgCommands.cpp

  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

md5sums=('834ef2a32fd173bd82224f22b60279ec')

# vim:set ts=2 sw=2 et:
