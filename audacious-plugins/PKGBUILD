# $Id: PKGBUILD 77107 2010-04-10 18:15:03Z ibiru $
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=2.4.0
pkgrel=1
pkgdesc="Plugins for Audacious"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL')
depends=('libmad' 'libvorbis')
makedepends=('jack' 'lame' 'fluidsynth' 'libcdio' 'libsidplay' 'wavpack'
             'libmpcdec' 'taglib' 'lirc-utils' 'curl' 'libmtp' 'sdl' 'projectm' 'imlib2'
             'neon' 'libmms' 'gtkglext' "audacious>=${pkgver}" 'ffmpeg' 'libsamplerate')
optdepends=('jack: JACK output plugin'
	    'libsamplerate: for JACK output'
            'lame: FileWriter output plugin'
            'fluidsynth: FluidSynth backend for AMIDI-Plug input plugin'
            'libcdio: CD Audio input plugin'
            'libsidplay: SID input plugin'
            'wavpack: WavPack input plugin'
            'libmpcdec: Musepack input plugin'
            'taglib: Musepack input plugin'
            'lirc-utils: LIRC plugin'
            'curl: Audioscrobbler plugin'
            'libmtp: MTP upload plugin'
            'sdl: Paranormal and projectM visualization plugins'
            'projectm: projectM visualization plugin'
            'imlib2: RootVis visualization plugin'
            'neon: HTTP transport plugin'
            'libmms: MMS transport plugin'
	    'ffmpeg: for MPEG 4 Audio AAC')
source=(http://distfiles.atheme.org/$pkgname-$pkgver.tgz)
sha256sums=('c5a9b5efe0315e4c34e0e1922a8852561ea5608974d0adb0f9ce91c20e743a13')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [[ $CARCH = "i686" ]]; then
    ./configure --prefix=/usr --disable-esd --disable-pulse \
      --enable-amidiplug --disable-adplug --enable-chardet \
      --disable-sse2
  elif [[ $CARCH = "x86_64" ]]; then
    ./configure --prefix=/usr --disable-esd --disable-pulse \
      --enable-amidiplug --disable-adplug --enable-chardet
  fi

  make
  make DESTDIR="$pkgdir" install
}
